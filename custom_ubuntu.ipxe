#!ipxe
###
### netboot.xyz-custom menu Ubuntu
###

clear custom_choice_ubuntu
set vNAME ubuntu
set vUBUNTU true

goto ${menu} ||

:custom_menu_${vNAME}
menu Ubuntu netinstall
item --gap Ubuntu
item ubuntu2204 ${space} 22.04 LTS (Jammy Jellyfish)
item ubuntu2004 ${space} 20.04 LTS (Focal Fossa)
item ubuntu1804 ${space} 18.04 LTS (Bionic Beaver) 
item ubuntu1604 ${space} 16.04 LTS (Xenial Xerus)
item ubuntu1404 ${space} 14.04 LTS (Trusty Tahr)
item backtomenu <- Back to menu
choose custom_choice_${vNAME}
echo ${cls}
goto ${custom_choice_ubuntu} || goto debug_${vNAME}

:ubuntu2204
set vRELEASE jammy
goto ${vNAME}

:ubuntu2004
set vRELEASE focal
goto ${vNAME}

:ubuntu1804
set vRELEASE bionic
goto ${vNAME}

:ubuntu1604
set vRELEASE xenial
goto ${vNAME}

:ubuntu1404
set vRELEASE trusty
goto ${vNAME}

:ubuntu
set vARCH amd64
iseq ${vRELEASE} focal && set vURL http://${vMIRROR}/ubuntu/dists/${vRELEASE}/main/installer-${vARCH}/current/legacy-images/netboot/ubuntu-installer/${vARCH} || set vURL http://${vMIRROR}/ubuntu/dists/${vRELEASE}-updates/main/installer-${vARCH}/current/images/netboot/ubuntu-installer/${vARCH}
console --x 1280 --y 1024
console --picture ${vURL}/boot-screens/splash.png
set vKERNEL ${vURL}/linux
set vINITRD ${vURL}/initrd.gz
set vOPTS 
goto os_install || goto debug_${vNAME}

:debug_ubuntu
echo +-------+
echo | DEBUG |
echo +-------+
echo custom_choice_${vNAME} = ${custom_choice_ubuntu}
echo vMIRROR = ${vMIRROR}
echo vCUSTOM_MIRROR = ${vCUSTOM_MIRROR}
echo vNAME = ${vNAME}
echo vUBUNTU = ${vUBUNTU}
echo vRELEASE = ${vRELEASE} 
echo vARCH = ${vARCH}
echo vURL = ${vURL}
echo vKERNEL = ${vKERNEL}
echo vINITRD = ${vINITRD}
echo vOPTS = ${vOPTS}
echo =========
goto error_${vNAME}

:error_ubuntu
echo +-------+
echo | ERROR |
echo +-------+
echo Error occured, press any key to return to menu ...
prompt
chain custom_${vNAME}.ipxe

:backtomenu
chain custom.ipxe || goto error_${vNAME}

:os_install
echo Booting ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
kernel ${vKERNEL} ${vOPTS} || goto debug_${vNAME}
initrd ${vINITRD} || goto debug_${vNAME}
boot || goto debug_${vNAME}
