#!ipxe
###
### netboot.xyz-custom menu Clonezilla
###

clear custom_choice_clonezilla
set vNAME clonezilla
set vCLONEZILLA true
set vARCH amd64
set vTYPE iso

goto ${menu} ||

:custom_menu_${vNAME}
menu Clonezilla netboot
item --gap Clonezilla
#item clonezilla_ubuntu_${vTYPE} ${space} 20191024-eoan (Ubuntu-based) (ISO)
#item clonezilla_debian_${vTYPE} ${space} 2.6.4-10 (Debian-based) (ISO)
item clonezilla_ubuntu ${space} 20191024-eoan (Ubuntu-based)
item clonezilla_debian ${space} 2.6.4-10 (Debian-based)
item backtomenu <- Back to menu
choose custom_choice_${vNAME}
echo ${cls}
goto ${custom_choice_clonezilla} || goto debug_${vNAME}

:clonezilla_ubuntu_iso
set vRELEASE 20191024-eoan
goto ${vNAME}_${vTYPE}

:clonezilla_debian_iso
set vRELEASE 2.6.4-10
goto ${vNAME}_${vTYPE}

:clonezilla_ubuntu
set vRELEASE 20191024-eoan
goto ${vNAME}

:clonezilla_debian
set vRELEASE 2.6.4-10
goto ${vNAME}

:clonezilla_iso
set vIMGFILE http://${vCUSTOM_MIRROR}/${custom_choice_clonezilla}/${vNAME}-live-${vRELEASE}-${vARCH}.${vTYPE}
goto ${vTYPE}_boot

:clonezilla
set vURL http://${vCUSTOM_MIRROR}/${custom_choice_clonezilla}
set vKERNEL ${vURL}/vmlinuz
set vINITRD ${vURL}/initrd.img
set vOPTS initrd=initrd.img fetch=${vURL}/filesystem.squashfs boot=live union=overlay username=user hostname=clonezilla config quiet components noswap edd=on nomodeset enforcing=0 locales= keyboard-layouts= ocs_live_run="ocs-live-general" ocs_live_extra_param="" ocs_live_batch="no" vga=788 ip= net.ifnames=0  splash i915.blacklist=yes radeonhd.blacklist=yes nouveau.blacklist=yes vmwgfx.enable_fbdev=1
goto os_install || goto debug_${vNAME}

:debug_clonezilla
echo +-------+
echo | DEBUG |
echo +-------+
echo custom_choice_${vNAME} = ${custom_choice_clonezilla}
echo vMIRROR = ${vMIRROR}
echo vCUSTOM_MIRROR = ${vCUSTOM_MIRROR}
echo vNAME = ${vNAME}
echo vCLONEZILLA = ${vCLONEZILLA}
echo vRELEASE = ${vRELEASE}
echo vARCH = ${vARCH}
echo vURL = ${vURL}
echo vKERNEL = ${vKERNEL}
echo vINITRD = ${vINITRD}
echo vOPTS = ${vOPTS}
echo vTYPE = ${vTYPE}
echo vIMGFILE = ${vIMGFILE}
echo =========
goto error_${vNAME}

:error_clonezilla
echo +-------+
echo | ERROR |
echo +-------+
echo Error occured, press any key to return to menu ...
prompt
chain custom_${vNAME}.ipxe

:backtomenu
chain custom.ipxe || goto error_${vNAME}

:iso_boot
echo Booting iso-file ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
initrd ${vIMGFILE} || goto debug_${vNAME}
chain memdisk vmalloc=512M iso raw || goto debug_${vNAME}
boot || goto debug_${vNAME}

:os_install
echo Booting ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
kernel ${vKERNEL} ${vOPTS} || goto debug_${vNAME}
initrd ${vINITRD} || goto debug_${vNAME}
boot || goto debug_${vNAME}
