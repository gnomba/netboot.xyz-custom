#!ipxe
###
### netboot.xyz-custom menu CentOS
###

clear custom_choice_centos
set vNAME centos
set vCENTOS true
set vARCH x86_64
set vRAMDISK ramdisk_size=3145728
set vPICTURE background/centos.png
console --picture ${vPICTURE}
# 1Mb - 1024kB
# 2Mb - 2048kB
# 4Mb - 4096kB
# 8Mb - 8192kB
# 16Mb - 16384kB
# 32Mb - 32768kB
# 64Mb - 65536kb
# 128Mb - 131072kB
# 256Mb - 262144kB
# 512Mb - 524288kB
# 1GB - 1048576kB
# 2GB - 2097152kB
# 3GB - 3145728kB
# 4GB - 4194304kB

# REPO ELREPO
#inst.addrepo=ELREPO-9,https://elrepo.org/linux/elrepo/el9/x86_64
#inst.addrepo=ELREPO-8,https://elrepo.org/linux/elrepo/el8/x86_64
#inst.addrepo=ELREPO-7,https://elrepo.org/linux/elrepo/el7/x86_64
#inst.addrepo=ELREPO-6,https://elrepo.org/linux/elrepo/el6/x86_64

# REPO EPEL
#inst.addrepo=EPEL-9,https://dl.fedoraproject.org/pub/epel/9/Everything/x86_64
#inst.addrepo=EPEL-8,https://dl.fedoraproject.org/pub/epel/8/Everything/x86_64
#inst.addrepo=EPEL-7,https://dl.fedoraproject.org/pub/epel/7/x86_64
#inst.addrepo=EPEL-6,https://archives.fedoraproject.org/pub/archive/epel/6/x86_64

# ADD DRIVERS
#inst.dd=hd:/dev/sdb:/kmod-be2net-12.0.0.0-7.el8_4.elrepo.x86_64.rpm
#inst.dd=cdrom:/dev/sr0:/kmod-be2net-12.0.0.0-7.el8_4.elrepo.x86_64.rpm

goto ${menu} ||

:custom_menu_${vNAME}
menu CentOS netinstall
item --gap
item --gap
item --gap
item --gap
item --gap
item --gap
item --gap CentOS
item centos9stream ${space} 9 (Stream)
item centos8stream ${space} 8 (Stream)
item centos8 ${space} 8 (8.5.2111)
item centos7 ${space} 7 (7.9.2009)
item centos6 ${space} 6 (6.10)
item backtomenu <- Back to menu
choose custom_choice_${vNAME}
echo ${cls}
goto ${custom_choice_centos} || goto debug_${vNAME}

:centos9stream
set vRELEASE 9-stream
set vMIRROR mirror.stream.centos.org
set vURL http://${vMIRROR}/${vRELEASE}/BaseOS/${vARCH}/os/images/pxeboot
set vPICTURE http://${vMIRROR}/${vRELEASE}/BaseOS/${vARCH}/os/isolinux/splash.png
set vOPTS biosdevname=0 net.ifnames=0 ${vRAMDISK} inst.repo=http://${vMIRROR}/centos/${vRELEASE}/BaseOS/${vARCH}/os inst.stage2=http://${vMIRROR}/${vRELEASE}/BaseOS/${vARCH}/os initrd=initrd.img ip=dhcp nameserver=8.8.4.4 inst.vnc inst.vncpassword=Pass123 
goto centos

:centos8stream
set vRELEASE 8-stream
set vMIRROR mirror.yandex.ru
set vURL http://${vMIRROR}/centos/${vRELEASE}/BaseOS/${vARCH}/os/images/pxeboot
set vPICTURE http://${vMIRROR}/${vRELEASE}/BaseOS/${vARCH}/os/isolinux/splash.png
set vOPTS biosdevname=0 net.ifnames=0 ${vRAMDISK} inst.repo=http://${vMIRROR}/centos/${vRELEASE}/BaseOS/${vARCH}/os initrd=initrd.img ip=dhcp nameserver=8.8.4.4 inst.vnc inst.vncpassword=Pass123 
goto centos

:centos8
set vRELEASE 8.5.2111
set vMIRROR archive.kernel.org
set vURL http://${vMIRROR}/centos-vault/${vRELEASE}/BaseOS/${vARCH}/os/images/pxeboot
set vPICTURE http://${vMIRROR}/centos-vault/${vRELEASE}/BaseOS/${vARCH}/os/isolinux/splash.png
set vOPTS biosdevname=0 net.ifnames=0 ${vRAMDISK} inst.repo=https://${vMIRROR}/centos/${vRELEASE}/BaseOS/${vARCH}/os initrd=initrd.img ip=dhcp nameserver=8.8.4.4 inst.vnc inst.vncpassword=Pass123
goto centos

:centos7
set vRELEASE 7.9.2009
set vMIRROR mirror.yandex.ru
set vURL http://${vMIRROR}/centos/${vRELEASE}/os/${vARCH}/images/pxeboot
set vPICTURE http://${vMIRROR}/centos/${vRELEASE}/os/${vARCH}/isolinux/splash.png
set vOPTS biosdevname=0 net.ifnames=0 ${vRAMDISK} repo=http://${vMIRROR}/centos/${vRELEASE}/os/${vARCH}/ initrd=initrd.img ip=dhcp nameserver=8.8.4.4 inst.vnc inst.vncpassword=Pass123
goto centos

:centos6
set vRELEASE 6.10
set vMIRROR archive.kernel.org
set vURL http://${vMIRROR}/centos-vault/${vRELEASE}/os/${vARCH}/images/pxeboot
#set vPICTURE http://${vMIRROR}/centos-vault/${vRELEASE}/os/${vARCH}/isolinux/splash.jpg
set vOPTS biosdevname=0 net.ifnames=0 ${vRAMDISK} repo=https://${vMIRROR}/centos/${vRELEASE}/os/${vARCH}/ initrd=initrd.img ip=dhcp nameserver=8.8.4.4 inst.vnc inst.vncpassword=Pass123
goto centos

:centos
#inst.vnc inst.vncpassword=<pass> inst.vncconnect=<ip>:5901
console --picture ${vPICTURE}
console --x 1280 --y 1024
set vKERNEL ${vURL}/vmlinuz
set vINITRD ${vURL}/initrd.img
goto os_install || goto debug_${vNAME}

:debug_centos
echo +-------+
echo | DEBUG |
echo +-------+
echo custom_choice_${vNAME} = ${custom_choice_centos}
echo vMIRROR = ${vMIRROR}
echo vCUSTOM_MIRROR = ${vCUSTOM_MIRROR}
echo vNAME = ${vNAME}
echo vCENTOS = ${vCENTOS}
echo vRELEASE = ${vRELEASE} 
echo vARCH = ${vARCH}
echo vURL = ${vURL}
echo vKERNEL = ${vKERNEL}
echo vINITRD = ${vINITRD}
echo vOPTS = ${vOPTS}
echo vPICTURE = ${vPICTURE}
echo =========
goto error_${vNAME}

:error_centos
echo +-------+
echo | ERROR |
echo +-------+
echo Error occured, press any key to return to menu ...
prompt
chain custom_${vNAME}.ipxe

:backtomenu
chain custom.ipxe || goto error_${vNAME}

:os_install
echo Booting ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
kernel ${vKERNEL} ${vOPTS} || goto debug_${vNAME}
initrd ${vINITRD} || goto debug_${vNAME}
boot || goto debug_${vNAME}
