#!ipxe
###
### netboot.xyz-custom menu
###

# background
set vPICTURE http://boot.ipxe.org/ipxe.png
console --x 1280 --y 1024
console --picture ${vPICTURE}

# mirror variables
set vMIRROR mirror.yandex.ru
set vCUSTOM_MIRROR 185.228.233.185

:custom
clear custom_choice
menu gnomba's bootloader

item --gap ArchLinux
item archlinux ${space} latest

item --gap CentOS
item centos8 ${space} 8 (8.1.1911)
item centos7 ${space} 7 (7.7.1908)
item centos6 ${space} 6 (6.10)

item --gap Clonezilla
item clonezilla_ubuntu ${space} 20191024-eoan (Ubuntu-based)
item clonezilla_debian ${space} 2.6.4-10 (Debian-based)

item --gap Debian
item debian10 ${space} 10 (Buster)
item debian9 ${space} 9 (Stretch)
item debian8 ${space} 8 (Jessie)
item debian7 ${space} 7 (Wheezy)
item debian11 ${space} (testing) 11 (Bullseye)
item debiansid ${space} (unstable) Sid

item --gap Fedora
#item fedora32 ${space} 32
item fedora31 ${space} 31
item fedora30 ${space} 30
item fedora29 ${space} 29

item --gap Gentoo
item gentoo_minimal ${space} (not work) 2020-02-16 minimal

item --gap GParted Live 1.1.0-1
#item gparted_iso ${space} (test ISO file)
item gparted_default ${space} Default settings
item gparted_kmslarge ${space} Default settings, KMS with large font
item gparted_kms ${space} Default settings, KMS (for ATI, Intel and NVIDIA VGA cards.)
item gparted_toram ${space} To RAM. Boot media can be removed later
item gparted_safegraphic ${space} Safe graphic settings without framebuffer, vga=normal
item gparted_failsafe ${space} Failsafe mode

item --gap Memtest
item memtest83 ${space} MemTest86 8.3 (only UEFI boot) (IMG)
item memtest437 ${space} MemTest86 4.3.7 (IMG)
item memtest501 ${space} Memtest86+ 5.01 (ISO)
item memtest420 ${space} Memtest86+ 4.20 (ISO)

item --gap OpenSUSE
item opensusetumbleweed ${space} Tumbleweed
item opensuse152 ${space} 15.2
item opensuse151 ${space} 15.1

item --gap SystemRescueCD
item systemrescuecd6 ${space} 6.0.7 (Arch-based)
item systemrescuecd5 ${space} 5.3.2 (Gentoo-based)

item custom_ubuntu Ubuntu
#item --gap Ubuntu
#item ubuntu2004 ${space} (not work) 20.04 LTS (Focal Fossa)
#item ubuntu1804 ${space} 18.04 LTS (Bionic Beaver) 
#item ubuntu1604 ${space} 16.04 LTS (Xenial Xerus)
#item ubuntu1404 ${space} 14.04 LTS (Trusty Tahr)

#item --gap Dell
#item dell ${space} Dell Support Live Image 2.2
item --gap Other boot
item sals ${space} SAL's NETWORK BOOT
item gentoolinux ${space} Gentoo minimal 2019-10-20
item ubuntulive ${space} (test ISO) Ubuntu Server Live 19.10
choose custom_choice || goto custom_exit
echo ${cls}
goto ${custom_choice}
goto custom_exit

:archlinux
set vNAME ArchLinux
set vRELEASE latest
set vARCH x86_64
set vURL http://${vMIRROR}/archlinux/iso/${vRELEASE}
set vKERNEL ${vURL}/arch/boot/${vARCH}/vmlinuz
set vINITRD ${vURL}/arch/boot/${vARCH}/archiso.img
set vOPTS vmlinuz initrd=archiso.img archiso_http_srv=${vURL}/ archisobasedir=arch verify=y ip=dhcp net.ifnames=0 ${console}
goto os_install

:centos8
set vRELEASE 8.1.1911
set vURL http://${vMIRROR}/centos/${vRELEASE}/BaseOS/${vARCH}/os/images/pxeboot
set vOPTS ramdisk_size=100000 repo=http://${vMIRROR}/centos/${vRELEASE}/BaseOS/${vARCH}/os ip=dhcp initrd=initrd.img
goto centos

:centos7
set vRELEASE 7.7.1908
set vURL http://${vMIRROR}/centos/${vRELEASE}/os/${vARCH}/images/pxeboot
set vOPTS ramdisk_size=100000 repo=http://${vMIRROR}/centos/${vRELEASE}/os/${vARCH}/ ip=dhcp initrd=initrd.img
goto centos

:centos6
set vRELEASE 6.10
set vURL http://${vMIRROR}/centos/${vRELEASE}/os/${vARCH}/images/pxeboot
set vOPTS ramdisk_size=100000 repo=http://${vMIRROR}/centos/${vRELEASE}/os/${vARCH}/ ip=dhcp initrd=initrd.img
goto centos

:centos
set vNAME CentOS
set vARCH x86_64
set vKERNEL ${vURL}/vmlinuz
set vINITRD ${vURL}/initrd.img
goto os_install

:clonezilla_ubuntu
set vRELEASE 20191024-eoan
goto clonezilla

:clonezilla_debian
set vRELEASE 2.6.4-10
goto clonezilla

:clonezilla
set vNAME clonezilla
set vARCH amd64
set vTYPE iso
set vIMGFILE http://${vCUSTOM_MIRROR}/${custom_choice}/${vNAME}-live-${vRELEASE}-${vARCH}.iso
goto ${vTYPE}_boot

:debian10
set vRELEASE buster
goto debian

:debian9
set vRELEASE stretch
goto debian

:debian8
set vRELEASE jessie
goto debian

:debian7
set vRELEASE wheezy
goto debian

:debian11
set vRELEASE bullseye
goto debian

:debiansid
set vRELEASE sid
goto debian

:debian
set vNAME Debian
set vARCH amd64
set vURL http://${vMIRROR}/debian/dists/${vRELEASE}/main/installer-${vARCH}/current/images/netboot/debian-installer/${vARCH}
set vKERNEL ${vURL}/linux
set vINITRD ${vURL}/initrd.gz
set vOPTS 
goto os_install

:fedora32
set vRELEASE 32
goto fedora

:fedora31
set vRELEASE 31
goto fedora

:fedora30
set vRELEASE 30
goto fedora

:fedora29
set vRELEASE 29
goto fedora

:fedora
set vNAME Fedora
set vARCH x86_64
set vURL http://${vMIRROR}/fedora/linux/releases/${vRELEASE}/Server/${vARCH}/os/images/pxeboot
set vKERNEL ${vURL}/vmlinuz
set vINITRD ${vURL}/initrd.img
set vOPTS ramdisk_size=100000 repo=http://${vMIRROR}/fedora/linux/releases/${vRELEASE}/Server/${vARCH}/os/ ip=dhcp initrd=initrd.img
goto os_install

:gentoo_minimal
set vRELEASE 2020-02-16
set vGENTOO true
set vNAME gentoo
set vARCH amd64
set vURL http://${vCUSTOM_MIRROR}/${vNAME}
set vKERNEL ${vURL}/${vNAME}
set vINITRD ${vURL}/${vNAME}.igz
set vOPTS ${vNAME} initrd=${vNAME}.igz root=/dev/ram0 init=/linuxrc loop=/image.squashfs looptype=squashfs cdroot=1 real_root=/ vga=791
goto os_install

#:gparted_iso
#set vRELEASE 1.1.0-1
#set vNAME gparted
#set vARCH amd64
#set vIMGFILE https://netcologne.dl.sourceforge.net/project/${vNAME}/${vNAME}-live-stable/${vRELEASE}/${vNAME}-live-${vRELEASE}-${vARCH}.iso
#goto iso_boot

:gparted_default
set vTMPOPTS 
goto gparted

:gparted_kmslarge
set vTMPOPTS vga=791 live_console_font_size=16x32
goto gparted

:gparted_kms
set vTMPOPTS vga=791
goto gparted

:gparted_toram
set vTMPOPTS toram=filesystem.squashfs
goto gparted

:gparted_safegraphic
set vTMPOPTS nomodeset vga=normal
goto gparted

:gparted_failsafe
set vTMPOPTS acpi=off irqpoll noapic noapm nodma nomce nolapic nosmp nomodeset vga=normal 
goto gparted

:gparted
set vRELEASE 1.1.0-1
set vNAME gparted
set vARCH amd64
set vURL http://${vCUSTOM_MIRROR}/${vNAME}/live
set vKERNEL ${vURL}/vmlinuz
set vINITRD ${vURL}/initrd.img
set vOPTS ${vTMPOPTS} initrd=initrd.img nosplash ip= net.ifnames=0 boot=live union=overlay username=user config components quiet noswap fetch=${vURL}/filesystem.squashfs
goto os_install

:memtest83
set vRELEASE 8.3
set vTYPE img
goto memtest

:memtest437
set vRELEASE 4.3.7
set vTYPE img
goto memtest

:memtest501
set vRELEASE 5.01
set vTYPE iso
goto memtest

:memtest420
set vRELEASE 4.20
set vTYPE iso
goto memtest

:memtest
set vNAME memtest
set vIMGFILE http://${vCUSTOM_MIRROR}/${vNAME}/${custom_choice}.${vTYPE}
goto ${vTYPE}_boot

:opensusetumbleweed
set vRELEASE tumbleweed
set vARCH x86_64
set vURL http://${vMIRROR}/opensuse/${vRELEASE}/repo/oss/boot/${vARCH}/loader
set vOPTS linux initrd=initrd splash=silent install=http://${vMIRROR}/opensuse/${vRELEASE}/repo/oss/
goto opensuse

:opensuse152
set vRELEASE 15.2
set vARCH x86_64
set vURL http://${vMIRROR}/opensuse/distribution/leap/${vRELEASE}/repo/oss/boot/${vARCH}/loader
set vOPTS linux initrd=initrd splash=silent install=http://${vMIRROR}/opensuse/distribution/leap/${vRELEASE}/repo/oss/
goto opensuse

:opensuse151
set vRELEASE 15.1
set vARCH x86_64
set vURL http://${vMIRROR}/opensuse/distribution/leap/${vRELEASE}/repo/oss/boot/${vARCH}/loader
set vOPTS linux initrd=initrd splash=silent install=http://${vMIRROR}/opensuse/distribution/leap/${vRELEASE}/repo/oss/
goto opensuse

:opensuse
set vNAME OpenSUSE
set vKERNEL ${vURL}/linux
set vINITRD ${vURL}/initrd
goto os_install

:systemrescuecd6
set vRELEASE 6.0.7
set vARCH x86_64
set vURL http://${vCUSTOM_MIRROR}/sysrcd6
set vKERNEL ${vURL}/sysresccd/boot/${vARCH}/vmlinuz
set vINITRD ${vURL}/sysresccd/boot/${vARCH}/sysresccd.img
set vOPTS vmlinuz initrd=sysresccd.img archiso_http_srv=${vURL}/ archisobasedir=sysresccd checksum=y ip=dhcp net.ifnames=0 ${console}
goto systemrescuecd

:systemrescuecd5
set vRELEASE 5.3.2
set vARCH amd64
set vURL http://${vCUSTOM_MIRROR}/sysrcd5
set vKERNEL ${vURL}/rescue64
set vINITRD ${vURL}/initram.igz
set vOPTS rescue64 setkmap=us dodhcp netboot=${vURL}/sysrcd.dat initrd=initram.igz
goto systemrescuecd

:systemrescuecd
set vNAME SystemRescueCD
goto os_install

:custom_ubuntu
chain https://raw.githubusercontent.com/${github_user}/netboot.xyz-custom/master/custom_ubuntu.ipxe || goto custom

:ubuntu2004
set vRELEASE focal
goto ubuntu

:ubuntu1804
set vRELEASE bionic
goto ubuntu

:ubuntu1604
set vRELEASE xenial
goto ubuntu

:ubuntu1404
set vRELEASE trusty
goto ubuntu

:ubuntu
set vNAME Ubuntu
set vARCH amd64
set vURL http://${vMIRROR}/ubuntu/dists/${vRELEASE}-updates/main/installer-${vARCH}/current/images/netboot/ubuntu-installer/${vARCH}
set vKERNEL ${vURL}/linux
set vINITRD ${vURL}/initrd.gz
set vOPTS 
goto os_install

:os_install
echo Booting ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
kernel ${vKERNEL} ${vOPTS}
isset ${vGENTOO} && initrd ${vURL}/image.squashfs
initrd ${vINITRD}
boot

:iso_boot
echo Booting iso-file ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
initrd ${vIMGFILE}
chain memdisk vmalloc=512M iso raw
boot

:img_boot
echo Booting img-file ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
initrd ${vIMGFILE}
chain memdisk vmalloc=16M raw
boot

:ubuntulive
imgfree
imgtrust --allow
initrd http://mirror.netcologne.de/ubuntu-isos/19.10/ubuntu-19.10-live-server-amd64.iso
chain http://boot.salstar.sk/memdisk iso raw
param sysuuid ${uuid}
boot || goto custom_exit

:gentoolinux
imgfree
imgtrust --allow
kernel http://boot.salstar.sk/memdisk
initrd http://mirror.yandex.ru/gentoo-distfiles/releases/amd64/autobuilds/20191020T214501Z/install-amd64-minimal-20191020T214501Z.iso
imgargs memdisk iso raw
boot || goto custom_exit

:dell
imgfree
kernel https://boot.netboot.xyz/memdisk bigraw iso
initrd --name SLI22_A00.iso http://192.168.1.29/SLI22_A00.iso
boot || goto custom_exit

:sals
chain http://boot.salstar.sk

:option_three
echo Chains into another menu...
chain https://raw.githubusercontent.com/${github_user}/netboot.xyz-custom/master/custom.ipxe || goto custom

:custom_exit
chain utils.ipxe
exit
