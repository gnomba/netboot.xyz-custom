#!ipxe
###
### netboot.xyz-custom menu
###

# background
set vPICTURE http://boot.ipxe.org/ipxe.png
console --x 1280 --y 1024
console --picture ${vPICTURE}

# mirror variables
set vMIRROR mirror.yandex.ru
set vCUSTOM_MIRROR 185.228.233.185

:custom
clear custom_choice
menu gnomba's bootloader

item --gap Clonezilla
item clonezilla_ubuntu ${space} 20191024-eoan (Ubuntu-based)
item clonezilla_debian ${space} 2.6.4-10 (Debian-based)

item --gap Fedora
#item fedora32 ${space} 32
item fedora31 ${space} 31
item fedora30 ${space} 30
item fedora29 ${space} 29

item --gap GParted Live 1.1.0-1
#item gparted_iso ${space} (test ISO file)
item gparted_default ${space} Default settings
item gparted_kmslarge ${space} Default settings, KMS with large font
item gparted_kms ${space} Default settings, KMS (for ATI, Intel and NVIDIA VGA cards.)
item gparted_toram ${space} To RAM. Boot media can be removed later
item gparted_safegraphic ${space} Safe graphic settings without framebuffer, vga=normal
item gparted_failsafe ${space} Failsafe mode

item --gap Memtest
item memtest83 ${space} MemTest86 8.3 (only UEFI boot) (IMG)
item memtest437 ${space} MemTest86 4.3.7 (IMG)
item memtest501 ${space} Memtest86+ 5.01 (ISO)
item memtest420 ${space} Memtest86+ 4.20 (ISO)

item custom_archlinux ArchLinux
item custom_centos CentOS
item custom_debian Debian
item custom_gentoo Gentoo
item custom_opensuse OpenSUSE
item custom_systemrescuecd SystemRescueCD
item custom_ubuntu Ubuntu

item --gap Other boot
item netbootxyz ${space} netboot.xyz NETWORK BOOT
item sals ${space} SAL's NETWORK BOOT

choose custom_choice || goto custom_exit
echo ${cls}
#iseq ${custom_choice} netbootxyz && goto netbootxyz
#iseq ${custom_choice} sals && goto sals
goto custom_menu
goto custom_exit

:clonezilla_ubuntu
set vRELEASE 20191024-eoan
goto clonezilla

:clonezilla_debian
set vRELEASE 2.6.4-10
goto clonezilla

:clonezilla
set vNAME clonezilla
set vARCH amd64
set vTYPE iso
set vIMGFILE http://${vCUSTOM_MIRROR}/${custom_choice}/${vNAME}-live-${vRELEASE}-${vARCH}.iso
goto ${vTYPE}_boot

:fedora32
set vRELEASE 32
goto fedora

:fedora31
set vRELEASE 31
goto fedora

:fedora30
set vRELEASE 30
goto fedora

:fedora29
set vRELEASE 29
goto fedora

:fedora
set vNAME Fedora
set vARCH x86_64
set vURL http://${vMIRROR}/fedora/linux/releases/${vRELEASE}/Server/${vARCH}/os/images/pxeboot
set vKERNEL ${vURL}/vmlinuz
set vINITRD ${vURL}/initrd.img
set vOPTS ramdisk_size=100000 repo=http://${vMIRROR}/fedora/linux/releases/${vRELEASE}/Server/${vARCH}/os/ ip=dhcp initrd=initrd.img
goto os_install || goto debug

#:gparted_iso
#set vRELEASE 1.1.0-1
#set vNAME gparted
#set vARCH amd64
#set vIMGFILE https://netcologne.dl.sourceforge.net/project/${vNAME}/${vNAME}-live-stable/${vRELEASE}/${vNAME}-live-${vRELEASE}-${vARCH}.iso
#goto iso_boot

:gparted_default
set vTMPOPTS 
goto gparted

:gparted_kmslarge
set vTMPOPTS vga=791 live_console_font_size=16x32
goto gparted

:gparted_kms
set vTMPOPTS vga=791
goto gparted

:gparted_toram
set vTMPOPTS toram=filesystem.squashfs
goto gparted

:gparted_safegraphic
set vTMPOPTS nomodeset vga=normal
goto gparted

:gparted_failsafe
set vTMPOPTS acpi=off irqpoll noapic noapm nodma nomce nolapic nosmp nomodeset vga=normal 
goto gparted

:gparted
set vRELEASE 1.1.0-1
set vNAME gparted
set vARCH amd64
set vURL http://${vCUSTOM_MIRROR}/${vNAME}/live
set vKERNEL ${vURL}/vmlinuz
set vINITRD ${vURL}/initrd.img
set vOPTS ${vTMPOPTS} initrd=initrd.img nosplash ip= net.ifnames=0 boot=live union=overlay username=user config components quiet noswap fetch=${vURL}/filesystem.squashfs
goto os_install

:memtest83
set vRELEASE 8.3
set vTYPE img
goto memtest

:memtest437
set vRELEASE 4.3.7
set vTYPE img
goto memtest

:memtest501
set vRELEASE 5.01
set vTYPE iso
goto memtest

:memtest420
set vRELEASE 4.20
set vTYPE iso
goto memtest

:memtest
set vNAME memtest
set vIMGFILE http://${vCUSTOM_MIRROR}/${vNAME}/${custom_choice}.${vTYPE}
goto ${vTYPE}_boot

:custom_menu
chain ${custom_choice}.ipxe || goto debug

:os_install
echo Booting ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
kernel ${vKERNEL} ${vOPTS}
isset ${vGENTOO} && initrd ${vURL}/image.squashfs
initrd ${vINITRD}
boot || goto debug

:iso_boot
echo Booting iso-file ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
initrd ${vIMGFILE}
chain memdisk vmalloc=512M iso raw
boot || goto debug

:img_boot
echo Booting img-file ${vNAME} ${vRELEASE} ${vARCH}
echo =================
imgfree
imgtrust --allow
initrd ${vIMGFILE}
chain memdisk vmalloc=16M raw
boot || goto debug

:netbootxyz
set conn_type https chain --autofree https://boot.netboot.xyz/menu.ipxe || echo HTTPS failed... attempting HTTP...
set conn_type http chain --autofree http://boot.netboot.xyz/menu.ipxe || echo HTTP failed, localbooting... exit 
goto custom

:sals
chain http://boot.salstar.sk || goto custom

:debug
echo +-------+
echo | DEBUG |
echo +-------+
echo custom_choice = ${custom_choice}
echo vMIRROR = ${vMIRROR}
echo vCUSTOM_MIRROR = ${vCUSTOM_MIRROR}
echo =========
goto error

:error
echo +-------+
echo | ERROR |
echo +-------+
echo Error occured, press any key to return to menu ...
prompt
chain custom.ipxe

:custom_exit
goto netbootxyz
exit
