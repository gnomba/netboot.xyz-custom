#!ipxe
###
### netboot.xyz-custom menu
###

# background
set vPICTURE background/ipxe.png
console --x 1280 --y 1024
console --picture ${vPICTURE}

# mirror variables
set vMIRROR mirror.yandex.ru
set vGH_MIRROR https://github.com/gnomba/netboot.xyz-custom/raw/master
set vSF_MIRROR 
###set vCUSTOM_MIRROR 185.228.233.185

:custom
clear custom_choice
menu gnomba's bootloader

item --gap Install:
item custom_archlinux ${space} ArchLinux
item custom_centos ${space} CentOS
item custom_debian ${space} Debian
item custom_fedora ${space} Fedora
item custom_gentoo ${space} Gentoo
item custom_opensuse ${space} OpenSUSE
item custom_scientific ${space} Scientific
item custom_ubuntu ${space} Ubuntu
item custom_windows ${space} Windows
#item custom_windows_v2 ${space} Windows_v2

item --gap Live:
#item custom_live_centos ${space} CentOS
item custom_live_clonezilla ${space} Clonezilla
item custom_live_gparted ${space} GParted
item custom_live_systemrescuecd ${space} SystemRescueCD

item --gap Tools:
item custom_tools_memtest ${space} Memtest

item --gap Other netboot:
item custom_netbootxyz ${space} netboot.xyz NETWORK BOOT
item custom_sals ${space} SAL's NETWORK BOOT

choose custom_choice || goto custom_exit
echo ${cls}
goto custom_menu || goto custom_exit

:custom_menu
chain ${custom_choice}.ipxe || goto debug

:debug
echo +-------+
echo | DEBUG |
echo +-------+
echo custom_choice = ${custom_choice}
echo vMIRROR = ${vMIRROR}
echo vCUSTOM_MIRROR = ${vCUSTOM_MIRROR}
echo =========
goto error

:error
echo +-------+
echo | ERROR |
echo +-------+
echo Error occured, press any key to return to menu ...
prompt
chain custom.ipxe

:custom_exit
goto netbootxyz
exit
